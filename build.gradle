import java.nio.file.Files
import java.nio.file.Paths
import org.gradle.internal.os.OperatingSystem
import edu.wpi.first.toolchain.*

plugins {
    id 'c'
    id 'cpp'
    id 'java-library'
    id 'visual-studio'
    id 'google-test-test-suite'
    id 'edu.wpi.first.NativeUtils' version '2020.9.4'
    id 'edu.wpi.first.GradleJni' version '0.10.1'
    id 'edu.wpi.first.GradleVsCode' version '0.11.0'
}

repositories {
    mavenCentral()
}

ext.licenseFile = file("$rootDir/LICENSE.txt")

apply from: 'config.gradle'

def outputsFolder = file("$buildDir/allOutputs")

task copyAllOutputs(type: Copy) {
    destinationDir outputsFolder
}

build.dependsOn copyAllOutputs

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archivePath
    copyAllOutputs.from task.archivePath
}

ext.getCurrentArch = {
    return NativePlatforms.desktop
}

model {
    components {
        wpimath(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDirs 'src/main/native/cpp'
                        include '**/*.cpp'
                        exclude '**/jni/**/*.cpp'
                    }
                    exportedHeaders {
                        srcDirs 'src/main/native/include', 'src/main/native/cpp/drake'
                    }
                }
            }
            binaries.all {
                cppCompiler.macros.put('BUILDING_WPIMATH', null)
                if (!(toolChain in VisualCpp)) {
                    cppCompiler.args.add('-fvisibility=hidden')
                }
            }
            appendDebugPathToBinaries(binaries)
        }
        wpimathjni(JniNativeLibrarySpec) {

            enableCheckTask false
            javaCompileTasks << compileJava
            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.roborio)
            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.raspbian)
            jniCrossCompileOptions << JniCrossCompileOptions(nativeUtils.wpi.platforms.aarch64bionic)
            sources {
                cpp {
                    source {
                        srcDirs 'src/main/native/cpp'
                        include '**/jni/**/*.cpp'
                    }
                    exportedHeaders {
                        srcDir 'src/main/native/include'
                        include '**/*.h'
                    }

                }
            }
            binaries.all {
                if (it instanceof StaticLibraryBinarySpec) {
                    it.buildable = false
                    return
                }
                lib library: 'wpimath', linkage: 'shared'
            }
        }
        wpimathDev(NativeExecutableSpec) {
            targetBuildTypes 'debug'
            sources {
                cpp {

                    source {
                        srcDirs 'src/dev/native/cpp'
                        include '**/*.cpp'
                    }
                    exportedHeaders {
                        srcDir 'src/main/native/include'
                    }
                }
            }
            binaries.all {
                lib library: 'wpimath', linkage: 'shared'
                lib library: 'wpimathjni', linkage: 'shared'
            }
        }
    }
    testSuites {
        cpptest(GoogleTestTestSuiteSpec) {
            for(NativeComponentSpec c : $.components) {
                testing c
                break
            }
            sources.cpp {
                source {
                    srcDirs 'src/test/native/cpp'
                    include '**/*.cpp'
                }
                exportedHeaders {
                    srcDirs 'src/test/native/include'
                }
            }
            nativeUtils.useRequiredLibrary(it, 'googletest')
        }
    }
    tasks {
        def c = $.components
        project.tasks.create('runCpp', Exec) {
            group = 'WPILib'
            description = "Run the wpimathDev executable"
            def found = false
            def systemArch = getCurrentArch()
            c.each {
                if (it in NativeExecutableSpec && it.name == 'wpimathDev') {
                    it.binaries.each {
                        if (!found) {
                            def arch = it.targetPlatform.name
                            if (arch == systemArch) {
                                dependsOn it.tasks.install
                                commandLine it.tasks.install.runScriptFile.get().asFile.toString()
                                def filePath = it.tasks.install.installDirectory.get().toString() + File.separatorChar + 'lib'
                                test.dependsOn it.tasks.install
                                test.systemProperty 'java.library.path', filePath
                                test.environment 'LD_LIBRARY_PATH', filePath
                                test.workingDir filePath
                                found = true
                            }
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'

    api "org.ejml:ejml-simple:0.38"
}

def wpilibNumberFileInput = file("src/generate/GenericNumber.java.in")
def natFileInput = file("src/generate/Nat.java.in")
def natGetterInput = file("src/generate/NatGetter.java.in")
def wpilibNumberFileOutputDir = file("$buildDir/generated/java/edu/wpi/first/math/numbers")
def wpilibNatFileOutput = file("$buildDir/generated/java/edu/wpi/first/math/Nat.java")
def maxNum = 20

task generateNumbers() {
    description = "Generates generic number classes from template"
    group = "WPILib"

    inputs.file wpilibNumberFileInput
    outputs.dir wpilibNumberFileOutputDir

    doLast {
        if(wpilibNumberFileOutputDir.exists()) {
            wpilibNumberFileOutputDir.delete()
        }
        wpilibNumberFileOutputDir.mkdirs()

        for(i in 0..maxNum) {
            def outputFile = new File(wpilibNumberFileOutputDir, "N${i}.java")
            def read = wpilibNumberFileInput.text.replace('${num}', i.toString())
            outputFile.write(read)
        }
    }
}

task generateNat() {
    description = "Generates Nat.java"
    group = "WPILib"
    inputs.files([natFileInput, natGetterInput])
    outputs.file wpilibNatFileOutput
    dependsOn generateNumbers

    doLast {
        if(wpilibNatFileOutput.exists()) {
            wpilibNatFileOutput.delete()
        }

        def template = natFileInput.text + "\n"

        def importsString = "";

        for(i in 0..maxNum) {
            importsString += "import edu.wpi.first.math.numbers.N${i};\n"
            template += natGetterInput.text.replace('${num}', i.toString()) + "\n"
        }
        template += "}\n" // Close the class body

        template = template.replace('{{REPLACEWITHIMPORTS}}', importsString)

        wpilibNatFileOutput.write(template)
    }
}

sourceSets.main.java.srcDir "${buildDir}/generated/java"
compileJava.dependsOn generateNumbers
compileJava.dependsOn generateNat

test {
    enabled = !(project.hasProperty('onlylinuxathena') || project.hasProperty('onlylinuxraspbian') || project.hasProperty('onlylinuxaarch64bionic'))

    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

apply from: 'publish.gradle'

wrapper {
    gradleVersion = '6.0.1'
}
